/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    provided
}

jar {
    manifest {
        attributes 'Main-Class': 'com.twosigma.beakerx.BeakerxDoclet'
    }
    doFirst {
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}

dependencies {
    compile files(org.gradle.internal.jvm.Jvm.current().toolsJar)
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    provided project(':kernel:base')
}

task jvmInspectJavaDoc(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = configurations.compile + configurations.provided
    options.doclet = "com.twosigma.beakerx.doclet.BeakerxDoclet"
    options.docletpath = [file("../doclet/build/libs/doclet.jar")]
    options.addStringOption("subpackages", "com.twosigma.beakerx")
    options.addStringOption("sourcepath", "../base/src/main/java")
    options.addStringOption("encoding", "UTF-8")
}

task moveInspectJson(type: Copy) {
    from("../doclet/beakerx_inspect.json")
    into("../../beakerx/beakerx/kernel/base/")
}

jvmInspectJavaDoc.finalizedBy moveInspectJson
build.finalizedBy jvmInspectJavaDoc

staticContent.enabled = false
cleanStaticContent.enabled = false
