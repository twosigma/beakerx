/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven-publish'

def final kernelIdName = 'groovy'
def final toReplace = '__PATH__'
def final mainClassToReplace = '__MAIN_CLASS__'

mainClassName = 'com.twosigma.beakerx.groovy.kernel.Groovy'

configurations {
  provided
  antlr
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
  test {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

repositories {
  mavenCentral()
}

dependencies {
  provided project(':base')
  compile group: "org.codehaus.groovy", name: "groovy-all", version: "2.4.3"
  compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.5'
  antlr group: 'org.antlr', name: 'antlr4', version: '4.5'

  testCompile project(':base').sourceSets.test.output
  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
  testCompile group: 'org.reflections', name: 'reflections', version: '0.9.10'
}

task antlr4_groovy_lexer(type: JavaExec) {
  main = "org.antlr.v4.Tool"
  workingDir = "src/main/antlr4/"
  args = ["GroovyLexer.g4", "-o", "../java/com/twosigma/beakerx/groovy/autocomplete", "-listener", "-no-visitor", "-package", "com.twosigma.beakerx.groovy.autocomplete", "-encoding", "UTF-8"]
  classpath = configurations.antlr
}
task antlr4_groovy_parser(type: JavaExec) {
  main = "org.antlr.v4.Tool"
  workingDir = "src/main/antlr4/"
  args = ["GroovyParser.g4", "-o", "../java/com/twosigma/beakerx/groovy/autocomplete", "-listener", "-no-visitor", "-package", "com.twosigma.beakerx.groovy.autocomplete", "-encoding", "UTF-8"]
  classpath = configurations.antlr
}
compileJava.dependsOn antlr4_groovy_lexer
compileJava.dependsOn antlr4_groovy_parser

jar {
  manifest {
    attributes "Main-Class": "$mainClassName"
  }
}

distZip.dependsOn installDist

task prepareJson(dependsOn: 'build') {
  doLast {
    println "Prepare JSON for Jupyter notebook"
    println "Copy kernel.json to build/libs/json/ and replacing PATH with groovy.jar path"
    def jarLocation = file('build/install/groovy/lib/').getAbsolutePath()
    jarLocation = jarLocation.replace("\\", "/")
    def text = file('kernel.json').text.replace(toReplace, jarLocation + "/*" + classPathSeparator() + pathToBaseLibs() + "/*").replace(mainClassToReplace, "$mainClassName")
    def folder = file('build/libs/json')
    if (!folder.exists()) {
      println "Creating folder build/libs/json"
      folder.mkdirs()
    }

    file('build/libs/json/kernel.json').withWriter { w ->
      w << text
    }
  }
}

task kernelInstall(dependsOn: 'prepareJson') {
  doLast {
    println "Installing 'groovy' to jupyter notebook"

    def hashStdOut = new ByteArrayOutputStream()
    def jsonFolder = file('build/libs/json/').getAbsolutePath();
    exec {
      commandLine "jupyter",
              "kernelspec",
              "install",
              "--sys-prefix",
              "--replace",
              "--name",
              kernelIdName,
              jsonFolder;
      standardOutput = hashStdOut
    }
    println hashStdOut.toString().trim();
    println "DONE"
  }
}

jacocoTestReport {
  reports {
    html.enabled = true
    xml.enabled = false
    csv.enabled = false
  }
  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
      fileTree(dir: it,
              exclude: ['com/twosigma/beakerx/groovy/autocomplete/GroovyParser*',
                        'com/twosigma/beakerx/groovy/autocomplete/GroovyLexer*'
              ])
    })
  }
}

task realclean
realclean.dependsOn(clean)
// should also remove
// GroovyLexer.java GroovyLexer.tokens GroovyParser.java GroovyParser.tokens
// GroovyParserBaseListener.java GroovyParserListener.java

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.twosigma'
      artifactId 'beaker-kernel-groovy'
      version '2.0-SNAPSHOT'

      from components.java
    }
  }
}
