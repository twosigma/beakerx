/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven-publish'

ext.kernelIdName = 'kotlin'
mainClassName = 'com.twosigma.beakerx.kotlin.kernel.Kotlin'

configurations {
  provided
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
  test {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

repositories {
  mavenCentral()
}

dependencies {
  provided project(':base')
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-compiler-embeddable', version: '1.3.21'
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-runtime', version: '1.3.21'
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.21'
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.21'

  testCompile project(':base').sourceSets.test.output
}

jar {
  manifest {
    attributes "Main-Class": "$mainClassName"
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.twosigma'
      artifactId 'beaker-kernel-kotlin'
      version '2.0-SNAPSHOT'

      from components.java
    }
  }
}

installDist.into new File(pathToStaticContent, kernelIdName)
distZip.dependsOn installDist
