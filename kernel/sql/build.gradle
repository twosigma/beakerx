/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'application'
apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


def final kernelIdName = 'sql'
def final toReplace = '__PATH__'

mainClassName = 'com.twosigma.beakerx.sql.SQLKernel'

repositories {
  mavenCentral()
}

dependencies {
  compile project(':kernel:base')
  compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version: '1.9.13'
  compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.1'
  //jdbc drivers
  compile group: 'com.h2database', name: 'h2', version: '1.4.193'

  testCompile project(':kernel:base').sourceSets.test.output
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
  manifest {
    attributes "Main-Class": "$mainClassName"
  }

  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
  }
}

task prepareJson(dependsOn: 'build') {
  doLast {
    println "Prepare sql JSON for Jupyter notebook"
    println "Copy kernel.json to build/libs/json/ and replacing PATH with sql.jar path"

    def jarLocation = file('build/libs/sql.jar').getAbsolutePath()
    jarLocation = jarLocation.replace("\\", "/");
    def text = file('kernel.json').text.replace(toReplace, jarLocation)
    def folder = file('build/libs/json')
    if (!folder.exists()) {
      println "Creating folder build/libs/json"
      folder.mkdirs()
    }

    file('build/libs/json/kernel.json').withWriter { w ->
      w << text
    }
  }
}

task kernelInstall(dependsOn: 'prepareJson') {
  doLast {
    println "Installing 'sql' to Jupyter notebook"

    def hashStdOut = new ByteArrayOutputStream()
    def jsonFolder = file('build/libs/json/').getAbsolutePath();
    exec {
      commandLine "jupyter",
              "kernelspec",
              "install",
              "--sys-prefix",
              "--replace",
              "--name",
              kernelIdName,
              jsonFolder;
      standardOutput = hashStdOut
    }
    println hashStdOut.toString().trim();
    println "DONE"
  }
}


