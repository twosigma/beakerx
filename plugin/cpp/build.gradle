/*
 *  Copyright 2014 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'java'

repositories {
  mavenCentral()
}

apply plugin:'application'
mainClassName = 'com.twosigma.beaker.cpp.Main'

configurations {
  provided
  antlr
  compile.extendsFrom provided
}

task makeClingLib(type: Exec, dependsOn: ['compileCppEvaluator', 'makeCppEvaluatorHeader']) {
  inputs.file('./src/main/cpp/ClingInterp.cpp')
  //outputs.file('./src/main/cpp/libClingInterp.jnilib')
  workingDir './src/main/cpp'
  commandLine 'make', 'all'
}

task copyClingInterp(type: Copy, dependsOn: installApp) {
  from './src/main/cpp/libClingInterp.jnilib'
  into evalPluginDir.getAbsolutePath() + '/cpp/lib'
}

task compileCppEvaluator(type: JavaCompile) {
  classpath = files(project(':plugin:jvm').projectDir)
  source 'com.twosigma.beaker.cpp.utils.CppEvaluator'
}

task makeCppEvaluatorHeader(type: Exec) {
  workingDir './src/main/java'
  commandLine 'javah', '-jni', '-o', '../cpp/CppEvaluator.h', 'com.twosigma.beaker.cpp.utils.CppEvaluator'
}

dependencies {
  provided project(':plugin:jvm')
  compile group: 'org.abstractmeta', name: 'compilation-toolbox', version: '0.3.2'
}

compileJava {
  dependsOn makeClingLib
  options.compilerArgs << '-Xlint:deprecation'
  options.compilerArgs << '-Xlint:unchecked'
}

if (hasProperty('evalPluginDir')) {
  installApp.into new File(evalPluginDir, 'cpp')
  installApp.finalizedBy copyClingInterp
}

if (hasProperty('includeDepsInJar')) {
  build.dependsOn jar
  jar {
    // need to remove META-INF/*.SF to use this.
    from {
       (configurations.compile - configurations.provided).collect { it.isDirectory() ? it : zipTree(it).matching{exclude{it.path.contains('META-INF') || it.path.contains('google')} } }
    } {
      exclude "META-INF/*.SF"
      exclude "META-INF/*.DSA"
      exclude "META-INF/*.RSA"
    }
  }
}

task realclean
realclean.dependsOn(clean)
// should also remove 
// Java.tokens JavaBaseListener.java JavaLexer.java JavaLexer.tokens JavaListener.java JavaParser.java       
